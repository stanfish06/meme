#!@WHICHPERL@
=head1 NAME

spamo_webservice - Run SpaMo in a restricted mode and create an index webpage.

=head1 SYNOPSIS

spamo_webservice [options] <sequences file> <primary motif> <secondary db patterns>

  Options:
    -bedfile          Sequence file is a BED file
    -genome <file>    Reference genome for BED file sequences
    -xalph            force expansion of the secondary db motif's alphabet to equal the primary.
    -uploaded <file>  file containing uploaded secondary motif database
    -margin <margin>  margin parameter passed to SpaMo 
    -dumpseqs         dump the sequence matches to a file for each significant primary/secondary
    -norc 	      treat DNA as single-stranded
    -help             brief help message

=cut

use strict;
use warnings;
# load standard perl libraries
use File::Basename qw(fileparse);
use File::Copy qw(move);
use File::Path qw(rmtree);
use File::Spec::Functions qw(catfile);
use Getopt::Long qw(:config permute auto_help);
use Pod::Usage;
# load custom perl libraries
use lib qw(@PERLLIBDIR@);
use Alphabet qw(rna dna protein);
use StatusPage qw(arg_checks arg_end opt_uploaded opt_db opt_choice opt_integer opt_number);
use Globals;
# constants
my $bin_dir = '@BINDIR@';
my $libexec_dir = '@LIBEXECDIR@';
my $motif_db_dir = '@MEMEDB@/motif_databases/';
my $fasta_db_dir = '@MEMEDB@/fasta_databases/';
my $dump_seqs_dir = 'dumpseqs';
my $dump_seqs_tar = 'dumpseqs.tar.gz';
my $dump_seqs_pattern = 'seqs_*.tsv';
# required parameters
my $sequences;
my $primary;
my @secondaries = ();
# option defaults
my $bedfile;
my $genome;
my $dumpseqs = 0; # FALSE
my $norc = 0; # FALSE
my $margin = undef;
my $xalph = 0;
my $bgfile = undef;
my @up_secondaries = ();
#status page
my $status = new StatusPage('SPAMO', \@ARGV);
$status->add_message('Parsing arguments');
# parse options
my @arg_errors = ();
my $opts_ok = do {
  local $SIG{__WARN__} = sub { my ($msg) = @_; chomp($msg); push(@arg_errors, $msg); };
  GetOptions(
  '<>' => arg_checks(
      opt_uploaded(\$sequences),
      opt_uploaded(\$primary),
      opt_db(\@secondaries, $motif_db_dir, 'motif_db')),
  'bedfile'     => \$bedfile,
  'genome=s'    => \$genome,
  'margin=i' => opt_integer(\$margin, 0), 
  'uploaded=s' => opt_uploaded(\@up_secondaries),
  'bgfile=s' => opt_uploaded(\$bgfile),
  'xalph' => \$xalph,
  'norc' => \$norc,
  'dumpseqs' => \$dumpseqs
  );
};
unshift(@secondaries, @up_secondaries) if @up_secondaries;
# add additional error messages for missing sequences and motifs
push(@arg_errors, "No sequences provided.") unless defined $sequences;
push(@arg_errors, "No primary motif provided.") unless defined $primary;
push(@arg_errors, "No secondary motifs provided.") unless @secondaries;
# Check bedfile related arguments
if (defined $bedfile || defined $genome) {
  if (! (defined $bedfile && defined $genome)) {
    push(@arg_errors, "The -bedfile, and -genome arguments must always be used together.");
  }
}
# display the error messages both to the status page and stderr
foreach my $arg_error (@arg_errors) {
  print STDERR $arg_error, "\n";
  $status->add_message($arg_error);
}
$opts_ok = 0 if (scalar(@arg_errors) > 0);
# setup status page
$status->add_file('html', 'spamo.html', 'SpaMo HTML output');
$status->add_file('tsv', 'spamo.tsv', 'SpaMo TSV output');
$status->add_file('xml', 'spamo.xml', 'SpaMo XML output');
$status->add_file('dump', $dump_seqs_tar, 'SpaMo dumped sequences');
if (defined $bedfile) {
  $status->add_file('bed', $sequences, 'Uploaded BED file');
  $status->add_file('seq', $sequences . '.fa', 'Sequences from BED file');
}
else {
  $status->add_file('seq', $sequences, 'Input sequences');
}
$status->add_file('pri', $primary, 'Input primary motif');
for (my $i = 0; $i < scalar(@up_secondaries); $i++) {
  $status->add_file('sec_' . $i , $up_secondaries[$i], 'Input secondary motifs (uploaded)');
}
$status->add_file('bgfile', $bgfile, 'Uploaded Background');
$status->add_message($opts_ok ? 'Arguments ok' : "Error parsing arguments");
$status->update($opts_ok ? 'Starting' : 'Error');
# exit if there was an error reading the arguments
unless ($opts_ok) {
  $status->write_log();
  pod2usage(2);
}
# create the symlink to the databases
symlink($motif_db_dir, 'motif_db');
symlink($fasta_db_dir, 'genome_db');
# ensure it will be removed on completion (when the log is written)
$status->set_cleanup( sub { unlink('motif_db'); } );
$status->set_cleanup( sub { unlink('genome_db'); } );

# If invoked with a bedfile option the sequence file is in BED format
# and we need to generate a FASTA file before passing it to MEME
if (defined $bedfile) {
  my @bedargs = ();
  push(@bedargs, '-s', '-both');
  push(@bedargs, '-o', $sequences . '.fa', $sequences);
  push(@bedargs,  'genome_db/' . $genome); 
  $status->run(PROG => 'bed2fasta', BIN => $libexec_dir, ARGS => \@bedargs);
  $sequences = $sequences . '.fa';
}
# Run SpaMo
my @spamo_args = ('-verbosity', 1, '-oc', '.', '-numgen', 1);
push(@spamo_args, ('-margin', $margin)) if $margin;
push(@spamo_args, ('-bgfile', $bgfile)) if $bgfile;
push(@spamo_args, '-dumpseqs') if $dumpseqs;
push(@spamo_args, '-norc') if $norc;
push(@spamo_args, '-xalph') if $xalph;
push(@spamo_args, $sequences, $primary,  @secondaries);
$status->run(PROG => 'spamo', BIN => $bin_dir, ARGS => \@spamo_args);

# tar dumpseqs output
if ($dumpseqs) {
  my @dseqs = glob($dump_seqs_pattern);
  my $count = 0;
  if (@dseqs) {
    mkdir $dump_seqs_dir;
    OUTER: for (my $i = 0; $i < scalar(@dseqs); $i++) {
      my $file = fileparse($dseqs[$i]);
      next if ($file eq $sequences || $file eq $primary);
      for (my $j = 0; $j < scalar(@up_secondaries); $j++) {
        next OUTER if ($file eq $up_secondaries[$j]);
      }
      move($file, catfile($dump_seqs_dir, $file));
      $count++;
    }
    if ($count) {
      $status->run(PROG => 'tar', ARGS => ['-czf', $dump_seqs_tar, $dump_seqs_dir]);
    }
    rmtree($dump_seqs_dir);
  }
}

# done
$status->add_message("Done");
$status->update();
$status->write_log();
1;
